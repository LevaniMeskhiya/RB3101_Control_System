<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_EventAlarm" Id="{60dca691-f0f0-402e-a989-be2affb3ce79}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_EventAlarm
VAR_INPUT
    bEvent         : BOOL;
    nEventID    : UDINT;
    sSource     : STRING;
END_VAR
VAR_OUTPUT
END_VAR
VAR
    bIsInitialized : BOOL;
    
    fbSourceInfo     : FB_TcSourceInfo;
    fbAlarm         : FB_TcAlarm;
    eventEntry        : TcEventEntry;
    fbRtEvent        : R_TRIG;
    fbFtEvent        : F_TRIG;
    
    hr                : HRESULT;
    hrLastError        : HRESULT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* Initialization *) 
    IF NOT bIsInitialized THEN
        bIsInitialized := TRUE;
        eventEntry := Tc_Events.MyEventClass.WarningEvent;
        eventEntry.nEventId := nEventID;
        
        fbSourceInfo.Clear();
        fbSourceInfo.sName := sSource;
        
        hr := fbAlarm.CreateEx(eventEntry, TRUE, ipSourceInfo := fbSourceInfo);
        
        IF FAILED(hr) THEN
            hrLastError := hr;
        END_IF
    END_IF
    
(* event trigger *)
    fbRtEvent(CLK := bEvent);
    fbFtEvent(CLK := bEvent);
    
(* alarm reised  *)
    IF fbRtEvent.Q THEN
        hr := fbAlarm.Raise(0);
        IF FAILED(hr) THEN
            hrLastError := hr;
        END_IF
    END_IF
    
(* alarm cleared *)
    IF fbFtEvent.Q THEN
        hr := fbAlarm.Clear(0, bResetConfirmation := TRUE);
        IF FAILED(hr) THEN 
            hrLastError := hr;
        END_IF
    END_IF]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>